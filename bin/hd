use HexDump::Tiny:ver<0.6>:auth<zef:raku-community-modules>;
my %*SUB-MAIN-OPTS = :named-anywhere;

subset File of Str where { .IO.f };

proto sub MAIN(|) {
    CATCH {
        MAIN "help";
        exit 1;
    }
    {*}
}

multi sub MAIN(File $file, *%_) {
    .say for hexdump slurp($file, :bin), |%_;
}

multi sub MAIN(*@strings, *%_) {
    .say for hexdump @strings.join(" "), |%_;
}

#| slurp from STDIN
multi sub MAIN(*%_) {
    .say for hexdump slurp(:bin), |%_;
}

multi sub MAIN(Bool :$help!) is hidden-from-USAGE {
    MAIN("help")
}
multi sub MAIN("help") is hidden-from-USAGE {
    my $name := $*PROGRAM-NAME;
    say qq:to/HELP/;
Provide a hexadecimal dump (v{ HexDump::Tiny.^ver })
---------------------------------
  $name <file>
  $name words on the command line
  $name   (read from STDIN)

Optional named arguments:
  --skip=Int       number of bytes to skip from beginning, default: 0
  --head=Int       number of bytes to show, default: all remaining
  --chunk-size=Int number of bytes to dump perl line, default: 16
HELP
}

# vim: expandtab shiftwidth=4
